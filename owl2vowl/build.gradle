plugins {
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'org.owasp.dependencycheck' version '12.1.3'
    id 'com.github.spotbugs' version '6.1.2'
    id 'war'
}

group = 'it.gov.innovazione.owl2vowl'
version = '0.0.1-SNAPSHOT'

def generatedVersionDir = "$projectDir/src/main/resources"

sourceCompatibility = '21'

repositories {
    mavenCentral()
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.yaml'
    }

    implementation 'org.springframework.boot:spring-boot-starter-log4j2'

    implementation group: 'commons-io', name: 'commons-io', version: '2.18.0'

    implementation('net.sourceforge.owlapi:owlapi-distribution:5.1.1') {
        exclude group: 'org.eclipse.rdf4j'
    }
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-model', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-model-api', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-model-vocabulary', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-languages', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-datatypes', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-binary', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-n3', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-nquads', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-ntriples', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-rdfjson', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-jsonld', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-rdfxml', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-trix', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-turtle', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-trig', version: '3.7.7'
    implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-hdt', version: '3.7.7'
    implementation('org.eclipse.rdf4j:rdf4j-rio-api:3.7.7') {
        exclude(group: "javax.xml.bind", module: "jaxb-api")
        exclude(group: "jakarta.xml.bind", module: "jakarta.xml.bind-api")
    }

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
    implementation group: 'com.google.guava', name: 'guava', version: '33.4.0-jre'

    providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

spotbugs {
    toolVersion = '4.8.6'
}

spotbugsMain {
    excludeFilter = file("${rootProject.projectDir}/config/spotbugs/exclude-filter.xml")

    reports {
        html {
            required = true
            outputLocation = file("${layout.buildDirectory.get()}/reports/spotbugs/main/spotbugs.html")
        }
    }
}

spotbugsTest {
    excludeFilter = file("${rootProject.projectDir}/config/spotbugs/exclude-filter.xml")
    reports {
        html {
            required = true
            outputLocation = file("${layout.buildDirectory.get()}/reports/spotbugs/test/spotbugs.html")
        }
    }
}

dependencyCheck {
    nvd {
        datafeedUrl = "https://dependency-check.github.io/DependencyCheck_Builder/nvd_cache"
        validForHours = 24
    }
    failOnError = false
    failBuildOnCVSS = 7
    skipConfigurations = ['spotbugs']
    analyzers {
        assemblyEnabled = false
        ossIndexEnabled = false
    }
    suppressionFile = "${rootProject.projectDir}/config/dependency-check/dependency-check-known-issues.xml"
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(build)) {
        dependencyCheckAnalyze.enabled = false
    }
}

war {
    enabled = false
}

bootWar {
    enabled = true
    from('../webVowl/deploy') {
        into('WEB-INF/classes/static')
    }
    archiveFileName.set("owl2vowl.war")
}

tasks.register("generateVersionProperties") {
    doLast {
        def propertiesFile = file("$generatedVersionDir/version.properties")
        def properties = new Properties()
        properties.setProperty("version", rootProject.version.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}

tasks.named("processResources") {
    dependsOn("generateVersionProperties")
}

tasks.named('test') {
    useJUnitPlatform()
}
